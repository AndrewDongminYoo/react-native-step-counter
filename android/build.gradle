buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0"
    }
}

boolean isNewArchitectureEnabled() {
    return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}

def getExtOrDefault(name) {
    return rootProject.ext.has(name)
        ? rootProject.ext.get(name)
        : project.properties["StepCounter_" + name]
}

def getExtOrIntegerDefault(name) {
    return getExtOrDefault(name).toInteger()
}

android {
    namespace "com.stepcounter"
    testNamespace 'com.stepcounter.test'
    ndkVersion getExtOrDefault("ndkVersion")
    compileSdk getExtOrIntegerDefault("compileSdk")

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdk getExtOrIntegerDefault("targetSdkVersion")
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    }

    buildTypes {
        release {
            // signingConfig signingConfigs.release
            minifyEnabled false
            versionNameSuffix 'prod'
        }
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix 'dev'
        }
    }

    lintOptions {
        disable "GradleCompatible"
        disable "DeprecatedAndroidGradlePlugin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            //noinspection GroovyImplicitNullArgumentCall
            if (isNewArchitectureEnabled()) {
                java.srcDirs += [
                    "src/newarch",
                    // This is needed to build Kotlin project with NewArch enabled
                    "${project.buildDir}/generated/source/codegen/java"
                ]
            } else {
                java.srcDirs += ["src/oldarch"]
            }
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
}

dependencies {
    // For < 0.71, this will be from the local maven repo
    // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
    //noinspection GradleDependency
    implementation 'com.facebook.react:react-android:0.71.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'androidx.core:core-ktx:1.9.0'
}

if (isNewArchitectureEnabled()) {
    react {
        jsRootDir = file("../src/")
        libraryName = "RNStepCounter"
        codegenJavaPackageName = "com.stepcounter"
    }
}
