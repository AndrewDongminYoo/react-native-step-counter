buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.4.1'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0'
  }
}

boolean isNewArchitectEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: 'org.jetbrains.kotlin.android'

ext {
  ndkVersion = '23.1.7779620'
  compileSdkVersion = 33
  buildToolsVersion = '33.0.1'
}

if (isNewArchitectEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["StepCounter_" + name]
}

def getExtOrIntegerDefault(name) {
  return getExtOrDefault(name).toInteger()
}

android {
  ndkVersion ndkVersion
  buildToolsVersion buildToolsVersion
  compileSdk 33

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectEnabled().toString()
    signingConfig signingConfigs.debug
  }

  buildTypes {
    release {
      // signingConfig signingConfigs.release
      minifyEnabled false
      versionNameSuffix 'prod'
    }
    debug {
      signingConfig signingConfigs.debug
      versionNameSuffix 'dev'
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
}

repositories {
  mavenCentral()
  google()
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation 'com.facebook.react:react-android:0.71.2'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
}

if (isNewArchitectEnabled()) {
  sourceSets {
    main {
      java.srcDirs += [
          // This is needed to build Kotlin project with NewArch enabled
          "${project.buildDir}/generated/source/codegen/java"
      ]
    }
  }
  react {
    jsRootDir = file("../src/")
    libraryName = "StepCounter"
    codegenJavaPackageName = "com.stepcounter"
  }
}
